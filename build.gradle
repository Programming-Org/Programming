import nu.studer.gradle.jooq.JooqEdition

buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.4.0'
    }
}

plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.6.1"
    id "application"
    id "nu.studer.jooq" version "6.0.1"
}

group 'io.github.org.programming'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava.options.encoding = "UTF-8"

sourceCompatibility = 17
targetCompatibility = 17

dependencies {
    implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-alpha.12'
    //Logger
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.11'
    //config
    implementation group: 'io.github.yusufsdiscordbot', name: 'config', version: '1.0.4'
    //json
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.3'
    //database
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.0'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.4.0'
    //jooq
    implementation group: 'org.jooq', name: 'jooq', version: '3.15.5'
    implementation group: 'org.jooq', name: 'jooq-codegen', version: '3.16.6'
    implementation group: 'org.jooq', name: 'jooq-meta', version: '3.16.6'
    //test
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    //jooq
    jooqGenerator "org.postgresql:postgresql:42.4.0"
}

apply plugin: 'jacoco' // code coverage reports


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}


jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
    }
    jacocoTestReport.dependsOn test
    jacocoTestCoverageVerification.dependsOn jacocoTestReport
}

configurations.all {
    //noinspection GrUnresolvedAccess
    // ez vulnerability fix
    exclude group: "org.slf4j", module: "slf4j-log4j12"
}

mainClassName = "io.github.org.programming.Bot"


compileJava {
    // Makes spotlessApply task run on every compile/build.
    dependsOn 'spotlessApply'

    // Nails the Java-Version of every Subproject
    sourceCompatibility = rootProject.sourceCompatibility
    targetCompatibility = rootProject.targetCompatibility
}


spotless {
    java {
        // Excludes build folder since it may contain compiled classes
        targetExclude("build/**")
        //No need to do spotless on generated code
        targetExclude("src/main/jooq/**")
        eclipse('4.21.0').configFile("${rootProject.rootDir}/meta/formatting/google-style-eclipse.xml")


        licenseHeader("""/*
 * Copyright 2022 Programming Org and other Programming Org contributors
 *
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 *
 * you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */ """)

    }
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jar {
    archiveVersion.set("")
}

/**
 * This task downloads all dependencies (with transitive dependencies) and puts them into the libraries folder.
 * Used instead of shadowJar to hopefully optimize build times.
 * Run the application jar with the downloaded library files in the classpath.
 */
task downloadDependencies {
    doLast {
        logger.info("===== Downloading dependencies =====")
        logger.info("  ---       Cleaning up...     ---")
        // delete superseded library jars
        Set<String> newFiles = sourceSets.main.runtimeClasspath.getFiles().stream().map(File::getName).toList()
        fileTree("libraries").files.stream().filter(file -> !newFiles.contains(file.getName())).forEach(file -> {
            logger.info("  -> Deleting leftover file: ${file.getName()}...")
            file.delete()
        })
        logger.info("  ---     Cleanup complete.    ---")

        logger.info("\n  ---  Copying dependencies... ---")
        copy {
            from(sourceSets.main.runtimeClasspath) {
                include "*.jar"
            }
            into 'libraries/'
            eachFile {
                logger.info("  -> Copying ${it.name}...")
            }
        }
        logger.info("  ---    Dependencies copied.  ---")
        logger.info("===== Dependencies downloaded ======")
    }
}

def props = new Properties()
//if the file exists, load the properties
if (file(".env").exists()) {
    file(".env").withInputStream { props.load(it) }
}


//create the build directory so no errors happen with the workflow
task createBuildDir {
    doLast {
        logger.info("===== Creating build directory =====")
        file("build").mkdirs()
        logger.info("===== Build directory created ======")
    }

    mustRunAfter(project.tasks.findByPath('clean'))
}


jooq {
    version = '3.15.2'
    edition = JooqEdition.OSS
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = props.getProperty("DB_URL", "null")
                    user = props.getProperty("DB_USER", "null")
                    password = props.getProperty("DB_PASSWORD", "null")
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''

                        forcedTypes {
                            forcedType {
                                name = 'INSTANT'
                                includeTypes = 'TIMESTAMP'
                            }
                        }
                    }
                    generate {
                        records = true
                        immutablePojos = true
                        fluentSetters = true

                    }
                    target {
                        packageName = 'io.github.org.programming.jooq'
                        directory = "src/main/jooq"
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}


check.dependsOn jacocoTestReport
